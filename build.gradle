plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'

	//Code scan
	id 'checkstyle'
	id 'pmd'
	id 'jacoco'
	id("com.github.spotbugs") version "6.0.13"
}

group = 'com.gabriel'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}


repositories {
	mavenCentral()
}

//Settings
bootJar {
	archiveFileName = 'gn-slot.jar'
}


//Tasks
tasks.named('test') {
	useJUnitPlatform()
	// finalizedBy jacocoTestReport - temporarily disabled due to Java 25 compatibility
}

checkstyle {
	configFile = rootProject.file("codequality/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = false
		html.required = true
	}
}

pmd {
	consoleOutput = false
	ruleSetFiles = files("codequality/pmd/rulesets.xml")
	ruleSets = []
}


spotbugs {
	excludeFilter.set(file("codequality/spotbugs/excludeFilter.xml"))
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
	launcher.set(javaToolchains.launcherFor {
		languageVersion.set(JavaLanguageVersion.of(21))
	})
}

// configure HTML report
spotbugsMain {
	ignoreFailures = false
	reports {
		xml.required.set(true)
		html {
			required.set(true)
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	ignoreFailures = false
	reports {
		xml.required.set(true)
		html {
			required.set(true)
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
		html.required = true
	}
}


jacocoTestReport {
	dependsOn test
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	spotbugs 'com.github.spotbugs:spotbugs:4.9.3'
	spotbugs 'org.apache.commons:commons-lang3:3.14.0'

	//Validation
	implementation('org.springframework.boot:spring-boot-starter-validation')

	//Swagger
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.1.0'

	//JAXB - XML Parsing
	implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.4'
	implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.0'
	implementation group: 'jakarta.activation', name: 'jakarta.activation-api', version: '2.1.2'
	implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '4.0.2'

	//Spotbugs
	implementation "com.github.spotbugs:spotbugs-annotations:4.1.2"

	//AOP
	implementation 'org.springframework.boot:spring-boot-starter-aop'










}



